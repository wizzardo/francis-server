apply plugin: 'maven-publish'
apply plugin: 'signing'

if (!project.hasProperty("ossrhUsername"))
    ext.ossrhUsername = ""
if (!project.hasProperty("ossrhPassword"))
    ext.ossrhPassword = ""

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                url = 'https://github.com/wizzardo/francis-server'
                description = project.ext.descriptionString

                scm {
                    connection = 'scm:git:git@github.com:wizzardo/francis-server.git'
                    developerConnection = 'scm:git:git@github.com:wizzardo/francis-server.git'
                    url = 'https://github.com/wizzardo/francis-server'
                }


                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'Wizzardo'
                        name = 'Mikhail Bobrutskov'
                        email = 'wizzardobox@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = uri('https://oss.sonatype.org/service/local/staging/deploy/maven2/')
            def snapshotsRepoUrl = uri('https://oss.sonatype.org/content/repositories/snapshots/')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }

        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
